///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Forgotten Armament NWN2 Style Weapons                                 //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Import Files                                                          //
///////////////////////////////////////////////////////////////////////////
COPY ~forgotten-armament/spl/MOSPR01.SPL~ ~override~ // +1d8 DMG on Crit Strike

///////////////////////////////////////////////////////////////////////////
// Review and Update all Weapons                                         //
///////////////////////////////////////////////////////////////////////////
OUTER_SPRINT speedfactor @20000
OUTER_SPRINT oldslash    @20001
OUTER_SPRINT oldpierc    @20002
OUTER_SPRINT oldcrush    @20003
OUTER_SPRINT oldslpie    @20004
OUTER_SPRINT oldmissi    @20005
OUTER_SPRINT newslash    @20006
OUTER_SPRINT newpierc    @20007
OUTER_SPRINT newcrush    @20008
OUTER_SPRINT newslpie    @20009
OUTER_SPRINT newmissi    @20010


OUTER_SPRINT bstddmgold  @20100
OUTER_SPRINT bstddmgnew  @20101
OUTER_SPRINT bstdcrit    @20102

OUTER_SPRINT speardmgold @20900
OUTER_SPRINT speardmgnew @20901
OUTER_SPRINT spearcrit   @20902


COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE  0x31 prof
  READ_SHORT 0x1c type
  READ_LONG 0x6a  fx_off ELSE 0
  READ_SHORT 0x6e fx_ind ELSE 0
  READ_SHORT 0x70 fx_num ELSE 0
  SPRINT item_name ~%SOURCE_FILE%~
  SET critmatch = 0
  SET critamount = 0

  // Bastard Swords
  PATCH_IF (prof = 89) BEGIN
    PATCH_PRINT ~%item_name% is a Bastard Sword~
    LPF ALTER_ITEM_HEADER INT_VAR header_type=1 dicesize=10 dicenumber=1 END
    FOR (index = 0 ; index < fx_num ; ++index) BEGIN
      READ_SHORT (fx_off + 0x30 * index) opcode
      PATCH_IF (opcode = 301) BEGIN
        SET critmatch = 1
        READ_LONG (fx_off + 0x30 * index + 4) param1
      END
    END

    PATCH_IF critmatch BEGIN
        param1 += 1
        LPF ALTER_EFFECT INT_VAR match_opcode = 301 parameter1 = param1 END
    END ELSE BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 301 target = 1 parameter1 = 1 parameter2 = 1 timing = 2 resist_dispel = 0 probability1 = 100 END
    END

    PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
      READ_LONG ~%offset%~ desc_strref
      PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
        READ_STRREF ~%offset%~ desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%oldslash%~ ~~
          REPLACE_TEXTUALLY ~%speedfactor%~ ~%newslash%~
          REPLACE_TEXTUALLY ~%bstddmgold%~ ~%bstddmgnew%~
          REPLACE_TEXTUALLY ~%speedfactor%~ ~%bstdcrit%~
        END
        SAY_EVALUATED ~%offset%~ ~%desc%~
      END
    END

  // Spears
  END ELSE PATCH_IF (prof = 98) BEGIN
    PATCH_PRINT ~%item_name% is a Spear~
    LPF ALTER_ITEM_HEADER INT_VAR header_type=1 dicesize=8 dicenumber=1 END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 341 target = 1 parameter2 = 1 timing = 2 resist_dispel = 0 probability1 = 100 STR_VAR resource = ~MOSPR01~ END
    
    PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
      READ_LONG ~%offset%~ desc_strref
      PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
        READ_STRREF ~%offset%~ desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%oldslpie%~ ~~
          REPLACE_TEXTUALLY ~%speedfactor%~ ~%newpierc%~
          REPLACE_TEXTUALLY ~%speardmgold%~ ~%speardmgnew%~
          REPLACE_TEXTUALLY ~%speedfactor%~ ~%spearcrit%~
        END
        SAY_EVALUATED ~%offset%~ ~%desc%~
      END
    END
  END

  BUT_ONLY