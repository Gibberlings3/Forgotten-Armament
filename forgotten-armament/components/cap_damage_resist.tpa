///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Forgotten Armament - Cap Physical Damage Resistance                   //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Import                                                                //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH resist IN
      1  // Slashing
      2  // Crushing
      3  // Piercing
      4  // Missile
    BEGIN
      COPY ~forgotten-armament/spl/MORES0%resist%.SPL~         ~override~
      COPY ~forgotten-armament/spl/MORES%resist%%resist%.SPL~  ~override~
      COPY ~forgotten-armament/eff/MORES0%resist%.EFF~         ~override~
      COPY ~forgotten-armament/spl/MORESC%resist%.SPL~         ~override~
    END

///////////////////////////////////////////////////////////////////////////
// Update SPLPROT.2da                                                    //
///////////////////////////////////////////////////////////////////////////
APPEND ~splprot.2da~ ~21_RESISTSLASHING>n%TAB%21%TAB%-1%TAB%3~
APPEND ~splprot.2da~ ~22_RESISTCRUSHING>n%TAB%22%TAB%-1%TAB%3~
APPEND ~splprot.2da~ ~23_RESISTPIERCING>n%TAB%23%TAB%-1%TAB%3~
APPEND ~splprot.2da~ ~24_RESISTMISSILE>n%TAB%24%TAB%-1%TAB%3~

// Update all imported files so they point in the correct direction.
COPY_EXISTING ~splprot.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 0 ~stat~

      // Slashing
      PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~21_RESISTSLASHING>n~ BEGIN 
        SET slash_res = %row%
      // Crushing
      END ELSE PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~22_RESISTCRUSHING>n~ BEGIN
        SET crush_res = %row%
      // Piercing
      END ELSE PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~23_RESISTPIERCING>n~ BEGIN
      	SET pierce_res = %row%
      // Missile
      END ELSE PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~24_RESISTMISSILE>n~ BEGIN
      	SET miss_res = %row%
      END
    END

// Slashing
COPY_EXISTING ~mores01.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = %dmg_rst% parameter2 = %slash_res% END
COPY_EXISTING ~mores11.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 86 parameter1 = %dmg_rst% END

// Crushing
COPY_EXISTING ~mores02.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = %dmg_rst% parameter2 = %crush_res% END
COPY_EXISTING ~mores22.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 87 parameter1 = %dmg_rst% END

// Piercing
COPY_EXISTING ~mores03.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = %dmg_rst% parameter2 = %pierce_res% END
COPY_EXISTING ~mores33.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 88 parameter1 = %dmg_rst% END

// Missile
COPY_EXISTING ~mores04.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter1 = %dmg_rst% parameter2 = %miss_res% END
COPY_EXISTING ~mores44.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 89 parameter1 = %dmg_rst% END